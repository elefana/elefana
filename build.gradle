buildscript {
	ext {
		springBootVersion = '1.5.6.RELEASE'
	}
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'io.spring.dependency-management'

	version = '1.0.0-SNAPSHOT'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	ext {
		elasticsearch5Version = '5.5.2'
		elasticsearch2Version = '2.4.4'
		hikariVersion = '2.7.1'
		jodaVersion = '2.9.9'
		jsoniterVersion = '0.9.16'
		codecVersion = '1.10'
	}

	repositories {
		mavenLocal()
		mavenCentral()
	}
}


project(":" + rootProject.name + "-core") {
	dependencies {
		compile('org.springframework.boot:spring-boot-starter-cache')
		compile('org.springframework.boot:spring-boot-starter-jdbc') {
			exclude group: 'org.apache.tomcat', module: 'tomcat-jdbc' 
		}
		compile('org.springframework.boot:spring-boot-starter-web')
		compile "com.jsoniter:jsoniter:$jsoniterVersion"
		compile "com.zaxxer:HikariCP:$hikariVersion"
		compile "joda-time:joda-time:$jodaVersion"
		compile "commons-codec:commons-codec:$codecVersion"
		compile('org.postgresql:postgresql')
		
		testCompile('org.springframework.boot:spring-boot-starter-test')
	}
}

project(":" + rootProject.name + "-es2") {
	apply plugin: 'org.springframework.boot'
	
	dependencies {
		compile project(":" + rootProject.name + "-core")
		compile "org.elasticsearch:elasticsearch:$elasticsearch2Version"

		testCompile "org.elasticsearch.client:rest:$elasticsearch5Version"
	}
}

project(":" + rootProject.name + "-es5") {
	apply plugin: 'org.springframework.boot'
	
	dependencies {
		compile project(":" + rootProject.name + "-core")
		compile "org.elasticsearch:elasticsearch:$elasticsearch5Version"

		testCompile "org.elasticsearch.client:rest:$elasticsearch5Version"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.1'
}
